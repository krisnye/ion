const
    ion = import '../'
    Statement = import './Statement'
    DynamicExpression = import './DynamicExpression'

export class ForInOfStatement extends Statement
    properties:
        toKey: (name) ->
            if @type is 'ForOfStatement'
                return parseInt(name)
            else
                return name
        forEach: (collection, callback) ->
            # console.log('forEach', collection)
            if @type is 'ForOfStatement'
                for let key = 0; key < collection.length; key++
                    let value = collection[key]
                    callback(key, value)
            else
                for key, value of collection
                    callback(key, value)
        activate: ->
            super

            if not @statements?
                @statements = {}            
                @valueName = @left.declarations[@type is 'ForOfStatement' ? 0 : 1]?.id.name
                @keyName = @left.declarations[@type is 'ForOfStatement' ? 1 : 0]?.id.name

            # we must create new contexts for every item in the list thingy.
            @collectionExpression ?= @context.createRuntime(@right)
            @unobserveExpression = @collectionExpression.observe(
                @collectionWatcher ?= (collection) =>
                    # console.log('collectionWatcher', collection)
                    if @collection isnt collection
                        @unobserveCollection?()
                        if @collection?
                            #   we cannot simply iterate the this.collection since it's values may have changed
                            #   this could result in orphaned statements.  We must remove all current statements.
                            for key of @statements
                                @removeItem(key)
                        @unobserveCollection = null
                        @collection = collection
                        if @collection?
                            @forEach(
                                @collection
                                (key, value) =>
                                    @addItem(key, value)
                            )
                            if @collectionExpression.mutable
                                @unobserveCollection = ion.observe(@collection, (@collectionObserver ?= @applyChanges.bind(@)))
            )
        deactivate: ->
            super
            @collectionWatcher?(undefined)
            @unobserveExpression()
            @unobserveCollection?()
        keyToUnicodeChar: (key) ->
            if typeof key is 'number'
                return String.fromCharCode(48 + key)
            else
                # we will cache a monotonically increasing index for this key
                @keyCache ?= {}
                @keyCacheCount ?= 0
                return @keyCache[key] ?= String.fromCharCode(48 + @keyCacheCount++)
        getOrderForKey: (key) -> @order + @keyToUnicodeChar(key)
        addItem: (key, value, activate = true) ->
            # console.log('addItem', key, value, activate)
            if @statements.hasOwnProperty(key)
                throw new Error("There should not be a current statement for this key: " + key)
            if value isnt undefined
                # see if there is a cached statement with that value
                let order = @getOrderForKey(key)
                let context = @context.newContext(undefined, order)
                if @valueName?
                    context.setVariableExpression(@valueName, new DynamicExpression({value:value}))
                if @keyName?
                    context.setVariableExpression(@keyName, new DynamicExpression({value:key}))
                let statement = context.createRuntime(@body)
                @statements[key] = statement
                if activate
                    statement.activate()
                return statement
        removeItem: (key, value) ->
            # console.log('removeItem', key, value)
            let statement = @statements[key]
            if statement?
                @disposeStatement(statement)
            delete @statements[key]
            return statement
        disposeStatement: (statement) ->
            # see if there is a remove function to call
            if @remove?
                let removeStatement = statement.context.createRuntime(@remove)
                removeStatement.activate()
                # now have to immediately deactivate the remove statement
                removeStatement.deactivate()
            statement.deactivate()
        summarize: (changes) ->
            let ignoreProperty = (name) =>
                if not name?
                    return true
                if name[0] is '_'
                    return true
                if name is 'length' and @type is 'ForOfStatement'
                    return true
                return false
            let map = new Map
            for {type,object,name,oldValue} in changes
                if object is @collection # if we are getting a late message from the old collection, disregard
                    if not ignoreProperty(name)
                        if not map.has(name)
                            map.set(name, {type,object,name,oldValue})
                        else
                            let change = map.get(name)
                            # update the type.
                            change.type = type
            let array = []
            map.forEach(
                (change, name, object)->
                    let newValue = change.object[name]
                    if newValue isnt change.oldValue
                        delete change.object
                        array.push(change)
            )
            return array
        applyChanges: (changes) ->
            let originalChanges = changes
            # prune the change messages down
            changes = @summarize(changes)
            if changes.length is 0
                return

            let getRecycleKey = (key, value) => @type is 'ForOfStatement' ? value : key

            # we can only use the fast recycle method IF there are no duplicate keys in the changes, and none are null
            let canRecycle = true
            #   since we implemented result sort ordering, we can now always recycle.

            # if canRecycle
            #     let checkRecycleKeys = new Map
            #     for {name, oldValue} in changes
            #         let checkKey = getRecycleKey(name, oldValue)
            #         if checkKey? and checkRecycleKeys.has(checkKey)
            #             canRecycle = false
            #             break

            if not canRecycle
                for {name,type,oldValue} in changes
                    let newValue = @collection?[name]
                    let key = @toKey(name)
                    if oldValue isnt undefined
                        @removeItem(key, oldValue)
                    if newValue isnt undefined
                        @addItem(key, newValue)
            else
                let recyclableStatements = new Map
                let activateStatements = []
                for {name,oldValue} in changes
                    let key = @toKey(name)
                    if oldValue isnt undefined
                        let rkey = getRecycleKey(key, oldValue)
                        let statement = @statements[key]
                        if statement?
                            delete @statements[key]
                            recyclableStatements.set(rkey, statement)

                let contextsToUpdateOrder = [] # [key,context]

                for {name,oldValue} in changes
                    let newValue = @collection?[name]
                    let key = @toKey(name)
                    if newValue isnt undefined
                        let rkey = getRecycleKey(key, newValue)
                        let statement = recyclableStatements.get(rkey)
                        # let statement = null
                        if statement?

                            # # now update any recycled statements
                            # for key, statement of recycledStatementsToUpdate
                            let context = statement.context
                            if @type is 'ForOfStatement'
                                if @keyName?
                                    context.variables[@keyName].setValue(key)
                                # we HAVE to wait until after we remove all old statements before updating the recycled statements
                                # otherwise, we might update a statements insertions to a position still occupied by an old statement
                                contextsToUpdateOrder.push([key,context])
                            else
                                if @valueName?
                                    context.variables[@valueName].setValue(newValue)

                            @statements[key] = statement
                            recyclableStatements.delete(rkey)
                        else
                            statement = @addItem(key, newValue, false)
                            if statement?
                                activateStatements.push(statement)

                # it's important that old statements are deactivated BEFORE new statements are activated

                # recyclableStatements are deactivated.
                recyclableStatements.forEach(
                    (statement) =>
                        @disposeStatement(statement)
                )

                # update recycled context orders
                for [key,context] in contextsToUpdateOrder
                    # We must now also update the new order on the context, since it may cause insertion locations to shift
                    let oldOrder = context.order
                    let newOrder = @getOrderForKey(key)
                    # console.log("{{oldOrder}} ----> {{newOrder}}")
                    context.order = @getOrderForKey(key)
                # all of these contexts share the same inserter, which is the same as the one in our context.  Now we update all orders simultaneously.
                @context.inserter?.update()

                # activate new statements
                for statement in activateStatements
                    statement.activate()

